apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    implementation project(':dirone:moduleone')
}


//apply plugin: cn.bingoogolapple.gradle.note.HelloGradlePlugin
apply plugin: 'hello-gradle-plugin'
outer {
    def mapOne = ['a': 'bc', 'b': 'cd']
    def mapTwo = ['c': 'de']

    name 'OuterExtension' // 通过 project.extensions.create('outer', OuterExtension) 创建的可以不用写等号
    innerOne {
        println 'innerOne delegate is ' + delegate
        name = 'OneInnerExtension'
        complex mapOne
        complex mapTwo
    }
    innerTwo {
        println 'innerTwo delegate is ' + delegate
        name = 'TwoInnerExtension'
        complex mapOne
        complex mapTwo
    }
    innerThree {
        println 'innerThree delegate is ' + delegate
        name 'ThreeInnerExtension' // 通过 project.outer.extensions.create('innerThree', InnerExtension) 创建的可以不用写等号
        complex mapOne
        complex mapTwo
    }
    complex 'abc', 'bcd'
    complex 'cde', 'def'
}